/*
	----------------------------------
	Script by: LGM Games
	----------------------------------
*/

game SetControlEnabled 0 ;

float speed = 0.0 ;
ship GetPlayer SetSpeed speed ;
ship GetPlayer SetPTE 0 ;
ship GetPlayer ForceStop 0 ;
ship GetPlayer SetInvulnerable 1 ;
ship GetByKeyName m14ridleySHPa SetInvulnerable 1 ;

/* <START BLOCK> */
float time = 1.0 ;
float3 black = 0.0 0.0 0.0 ;
camera FadeToColor black time ;
pause 1.0 ;
game ShowAllPanels 0 ;
screen ShowLine 1 ;
camera SetCSCamera 1 ;
camera GetDefault ;

/* <ACTORS> */

float3 posRidley ;
waypoint GetByKeyName m14marker GetPosition posRidley ;
ship GetByKeyName m14ridleySHPa SetPosition posRidley ;
ship GetByKeyName m14ridleySHPa SetSpeed speed ;
ship GetByKeyName m14ridleySHPa SetPTE 0 ;

float3 posPlayer ;
float3 posAdd = 40.0 -25.0 0.0 ;
math posPlayer = posRidley + posAdd ;
ship GetPlayer SetPosition posPlayer ;
ship GetPlayer VectorTo posRidley ;
ship GetByKeyName m14ridleySHPa VectorTo posPlayer ;

/* <FADE FROM BLACK> */
time = 1.0 ;
black = 0.0 0.0 0.0 ;
camera FadeFromColor black time ;

/* <CAMERA 1> */
/* rotation -> ridley */
int objectType = 1 ;
float rotStart = -3.4 ;
float rotSpeed = 0.08 ;
float vertStart = -2.0 ;
float vertOffset = 12.0 ;
float vertSpeed = 0.06 ;
float camDistance = 120.0 ;
camera SetType 5 objectType m14ridleySHPa rotStart rotSpeed vertStart vertOffset vertSpeed camDistance ;

time = 3.0 ; 
float zoomAmount = 60.0 ; 
camera SetZoom 0 time zoomAmount ;

pause 3.0 ;

float3 goToRidley ;
posAdd = 500.0 -20.0 0.0 ;
math goToRidley = posRidley + posAdd ;
ship GetByKeyName m14ridleySHPa GoTo goToRidley ;

/* <CAMERA 2> */
/* rotation player */
objectType = 0 ;
rotStart = 3.4 ;
rotSpeed = -0.08 ;
vertStart = 2.0 ;
vertOffset = 6.0 ;
vertSpeed = 0.04 ;
camDistance = 40.0 ;
camera SetType 5 objectType NONE rotStart rotSpeed vertStart vertOffset vertSpeed camDistance ;

time = 6.0 ; 
zoomAmount = 160.0 ; 
camera SetZoom 0 time zoomAmount ;

/* script show T-Jump effect bubble or something */

float3 playerGoTo ;
posAdd = -500.0 0.0 0.0 ;
math playerGoTo = posPlayer + posAdd ;
ship GetPlayer GoTo playerGoTo ;

/* particle effect IDs depending on ship size */
float shipRadius = 1.0 ;
ship GetPlayer GetRadius shipRadius ;

int tunnelId = 211 ;
int flashId = 210 ;
if shipRadius > 0.01 && shipRadius < 10.01
	tunnelId = 211 ;
endif
if shipRadius > 10.01 && shipRadius < 20.01
	tunnelId = 212 ;
endif
if shipRadius > 20.01 && shipRadius < 100.01
	tunnelId = 213 ;
endif

/* equipment asb -> FreeModel.wds */
int retId = 0 ;
float delay = 0.0 ;
float3 position = 0.0 0.0 0.0 ;
float lifetime = 6.0 ;
assemblyex Create retId delay position tunnelId lifetime ;

pause 1.5 ;

/* TDrive particle effect */
int retId2 = 1 ;
delay = 0.0 ;
position = 0.0 0.0 0.0 ;
ship GetPlayer GetPosition position ;

/* adjust flash position according to camera */
float3 posCamera = 0.0 0.0 0.0 ;
float3 e_posCC = 0.0 0.0 0.0 ;
float3 posFlash = 0.0 0.0 0.0 ;
camera VectorTo posCamera ;
math e_posCC = posCamera * shipRadius ;
math posCamera = e_posCC * 0.6 ;
math posFlash = position + posCamera ;

lifetime = 2.0 ;
assembly delay posFlash flashId lifetime ;

pause 1.5 ;

/* END BLOCK */
/*
time = 1.0 ;
camera FadeToColor black time ;
pause 1.0 ;

game ShowAllPanels 1 ;
screen ShowLine 0 ;

camera SetDefault ;
camera SetCSCamera 0 ;
camera FadeFromColor black time ;
game SetControlEnabled 1 ;

ship GetPlayer SetInvulnerable 0 ;
ship GetByKeyName m14ridleySHPa SetInvulnerable 0 ;
*/

/* <TRIGGER SCRIPT OR DIALOG> */
/*
pause 1.0 ;
*/
ship GetPlayer SetInvulnerable 0 ;
ship GetByKeyName m14ridleySHPa SetInvulnerable 0 ;

game ShowAllPanels 1 ;
screen ShowLine 0 ;
camera SetDefault ;
camera SetCSCamera 0 ;

float3 positionNew = 0.0 0.0 0.0 ;
waypoint GetByKeyName m14aWp GetPosition positionNew ;
player SetPosition 1 positionNew ; /* teleport via TDrive */

End
