/*
-------
Script by: LGM Games, 2013  
+default structure OnDestroy script
+used automatically if no override script supplied in object parameters
+regions [80, 94]
-------
+experience
	+extraExp ifGunner
+deactivate
+reduce reputation
+chance to draw security (region specific ships & factions)
	+one-liner dialogue
	+notification
+specific blocks depending on StructureSort
	+mining facility = chance to drop ore loot
	+nebula collector = chance to drop gas loot
	+amp = chance to trigger global event anomalies
	+prison = chance to trigger global event pirates
*/

/* DEFINE VARIABLES FOR LATER USE */
int titleLine = 1 ;
int textLine1 = 2 ;
int textLine2 = -1 ;
int eventChance = 1 ;
int regionId = 0 ;
float modifier = 2.0 ;
int modObj = 0 ;
float eventDur = 60.0 ;
int placeType = 1 ;
int eventId = 0 ;
int lootId = 0 ;
float3 lootPos = 0.0 0.0 0.0 ;
float3 structPos = 0.0 0.0 0.0 ;
float3 addedPos = 400.0 0.0 400.0 ;
structure GetParent GetPosition structPos ;
math lootPos = structPos + addedPos ;

/* |---------------- EXPERIENCE ----------------| */

int expBase = 150 ;
int expAdd = 0 ;
int experience = 50 ;
int expClass = 0 ;

/* additional exp rewarded per experience level */
int level = 0 ;
player GetLevel level ;
math expAdd = 10 * level ;
math expClass = expBase + expAdd ;

/* exp reward increased if character class is Gunner */
int class = 0 ;
player GetCharacterClass class ;
if class == 2
	math experience = expClass * 2 ;
else
	math experience = expClass * 1 ;
endif

/* add final exp reward */
player AddExp experience ;

/* |---------------- EXPERIENCE ----------------| */

/* |---------------- DEACTIVATE ----------------| */
structure GetParent CallEvent OnDeactivate ;
/* |---------------- DEACTIVATE ----------------| */

/* |---------------- REPUTATION ----------------| */

int facParent = 0 ;
int repChange = -50 ; 
structure GetParent GetFaction facParent ;
/* faction-dependant reputation gain +/- */
if facParent < 36
	repChange = -50 ;
	if facParent > 7 && facParent < 10
		repChange = 50 ;
	endif
else
	repChange = 50 ;
endif
if facParent == 2
	repChange = 50 ;
endif

player AddReputation repChange ;
/* |---------------- REPUTATION ----------------| */

/* |---------------- SECURITY ----------------| */
int chance = 0 ;
math chance = Rnd 1 4 ;
if chance == 1

	/* DEFINE VARIABLES USED LATER */
	int spawnType = 0 ; /* region based */
	int faction = 0 ;
	int facBase = 0 ;
	structure GetParent GetFaction facBase ;
	int shipId = 0 ;
	int shipX = 0 ; /* local region faction ship ID min | always stays the same  */
	int shipY = 0 ; /* local region faction ship ID max | changes depending on region and faction */
	int number = 0 ;
	math number = Rnd 1 3 ; /* random chance for multiple ships to appear */
	float3 posStart = 0.0 0.0 0.0 ;
	float3 posMid = 0.0 50.0 700.0 ;
	float3 spawnPos1 = 0.0 0.0 0.0 ;
	float3 spawnPos2 = 0.0 0.0 0.0 ;
	structure GetParent GetPosition posStart ;
	math spawnPos1 = posMid + posStart ;
	math spawnPos2 = posMid - posStart ;
	int dialogLine = 339 ;
	math dialogLine = Rnd 339 348 ;
	float dialogDur = 5.0 ;
	int facDiaId = 0 ;
	math facDiaId = facBase + 1000 ;
	
	/* assign region based parameters */
	region GetId regionId ;
	if regionId == 80
		if facBase == 45
			faction = 0 ;
			shipY = 4 ;
		endif
		if facBase == 2
			faction = 1 ;
			shipY = 3 ;
		endif
	endif
	
	if regionId == 81
		shipY = 2 ;
		if facBase == 43
			faction = 0 ;
			shipY = 4 ;
		endif
		if facBase == 2
			faction = 1 ;
			shipY = 3 ;
		endif
		if facBase == 3
			faction = 2 ;
		endif
		if facBase == 5
			faction = 3 ;
		endif
	endif
	
	if regionId == 82
		if facBase == 44
			faction = 0 ;
			shipY = 3 ;
		endif
		if facBase == 10
			faction = 1 ;
			shipY = 4 ;
		endif
	endif
	
	if regionId == 83
		shipY = 4 ;
		if facBase == 51
			faction = 0 ;
		endif
		if facBase == 6
			faction = 1 ;
			shipY = 1 ;
		endif
		if facBase == 11
			faction = 2 ;
		endif
	endif
	
	if regionId == 84
		shipY = 2 ;
		if facBase == 46
			faction = 0 ;
		endif
		if facBase == 6
			faction = 1 ;
			shipY = 1 ;
		endif
		if facBase == 7
			faction = 2 ;
		endif
	endif
	
	if regionId == 85
		if facBase == 40
			faction = 0 ;
			shipY = 4 ;
		endif
		if facBase == 7
			faction = 1 ;
			shipY = 2 ;
		endif
		if facBase == 18
			faction = 2 ;
			shipY = 1 ;
		endif
	endif
	
	if regionId == 86
		shipY = 3 ;
		if facBase == 49
			faction = 0 ;
			shipY = 2 ;
		endif
		if facBase == 14
			faction = 1 ;
		endif
		if facBase == 53
			faction = 2 ;
		endif
	endif
	
	if regionId == 87
		shipY = 4 ;
		if facBase == 50
			faction = 0 ;
		endif
		if facBase == 11
			faction = 1 ;
		endif
		if facBase == 13
			faction = 2 ;
		endif
	endif
	
	if regionId == 88
		shipY = 3 ;
		if facBase == 38
			faction = 0 ;
		endif
		if facBase == 13
			faction = 1 ;
			shipY = 4 ;
		endif
		if facBase == 14
			faction = 2 ;
		endif
	endif
	
	if regionId == 89
		if facBase == 42
			faction = 0 ;
			shipY = 5 ;
		endif
		if facBase == 16
			faction = 1 ;
			shipY = 1 ;
		endif
		if facBase == 47
			faction = 2 ;
			shipY = 6 ;
		endif
	endif
	
	if regionId == 90
		if facBase == 47
			faction = 0 ;
			shipY = 6 ;
		endif
		if facBase == 16
			faction = 1 ;
			shipY = 1 ;
		endif
		if facBase == 17
			faction = 2 ;
			shipY = 3 ;
		endif
	endif
	
	if regionId == 91
		if facBase == 47
			faction = 0 ;
			shipY = 6 ;
		endif
		if facBase == 17
			faction = 1 ;
			shipY = 3 ;
		endif
		if facBase == 42
			faction = 2 ;
			shipY = 5 ;
		endif
	endif
	
	if regionId == 92
		if facBase == 42
			faction = 0 ;
			shipY = 5 ;
		endif
		if facBase == 47
			faction = 1 ;
			shipY = 6 ;
		endif
	endif
	
	if regionId == 93
		shipY = 2 ;
		if facBase == 7
			faction = 0 ;
		endif
		if facBase == 46
			faction = 1 ;
		endif
	endif
	
	if regionId == 94
		if facBase == 42
			faction = 0 ;
			shipY = 5 ;
		endif
		if facBase == 10
			faction = 1 ;
			shipY = 4 ;
		endif
		if facBase == 16
			faction = 2 ;
			shipY = 1 ;
		endif
	endif
	
	if regionId == 95
		if facBase == 9
			faction = 0 ;
			shipY = 4 ;
		endif
		if facBase == 8
			faction = 1 ;
			shipY = 4 ;
		endif
	endif
	
	/* ----- Spawn structure security ----- */
	int checkExist = 0 ;
	ship GetByKeyName structSecurity1 Exist checkExist ;
	if checkExist != 1
		PrintMessage Spawn_structSecurity1! ;
		math shipId = Rnd shipX shipY ;
		shipAdd structSecurity1 spawnType spawnPos1 faction shipId ;
		ship GetByKeyName structSecurity1 AttackPlayer ;
	endif
	
	if number == 2
		ship GetByKeyName structSecurity2 Exist checkExist ;
		if checkExist != 1
			PrintMessage Spawn_structSecurity2! ;
			math shipId = Rnd shipX shipY ;
			shipAdd structSecurity2 spawnType spawnPos2 faction shipId ;
			ship GetByKeyName structSecurity2 AttackPlayer ;
		endif
	endif
	
	/* one-liner dialogue */
	factdialogue facDiaId dialogLine dialogDur ;
	
endif
/* |---------------- SECURITY ----------------| */

/* |---------------- SPECIFIC ----------------| */

int structSort = 0 ;
structure GetParent GetSort structSort ;

/* mining facility */
if structSort == 6
	/* loot drop */	
	lootId = 8 ;
	loot LootDrop lootId lootPos ;
	/* notification */
	titleLine = 886 ;
	textLine1 = 936 ;
	math textLine1 = Rnd 923 925 ;
	textLine2 = -1 ;
	screen ShowWarnings titleLine textLine1 textLine2 ;
endif

/* nebula collector */
if structSort == 7
	/* loot drop */
	lootId = 9 ;
	loot LootDrop lootId lootPos ;
	/* notification */
	titleLine = 885 ;
	textLine1 = 936 ;
	math textLine1 = Rnd 923 925 ;
	textLine2 = -1 ;
	screen ShowWarnings titleLine textLine1 textLine2 ;
endif

/* amp */
if structSort == 10
	/* chance to trigger anomaly activity increased event */
	math eventChance = Rnd 1 5 ;
	if eventChance == 1
		eventId = 17 ;
		titleLine = 976 ;
		region GetId regionId ;
		math eventDur = Rnd 600.0 1200.0 ;
		math modifier = Rnd 1.5 4.0 ;
		math textLine = Rnd 923 925 ;
		globalevent Set eventId placeType regionId eventDur modObj modifier titleLine textLine ;
		globalevent AddLog eventId placeType regionId eventDur modObj modifier titleLine textLine ;
	endif
	/* notification */
	titleLine = 892 ;
	textLine1 = 940 ;
	textLine2 = -1 ;
	screen ShowWarnings titleLine textLine1 textLine2 ;
endif

/* prison */
if structSort == 11
	/* chance to trigger pirate activity increased event */
	math eventChance = Rnd 1 5 ;
	if eventChance == 1
		eventId = 8 ;
		titleLine = 971 ;
		region GetId regionId ;
		math eventDur = Rnd 600.0 1200.0 ;
		math modifier = Rnd 1.5 4.0 ;
		math textLine = Rnd 907 911 ;
		globalevent Set eventId placeType regionId eventDur modObj modifier titleLine textLine ;
		globalevent AddLog eventId placeType regionId eventDur modObj modifier titleLine textLine ;
	endif
	
	/* notification */
	titleLine = 891 ;
	textLine1 = 941 ;
	textLine2 = -1 ;
	screen ShowWarnings titleLine textLine1 textLine2 ;
endif

/* |---------------- SPECIFIC ----------------| */

End
